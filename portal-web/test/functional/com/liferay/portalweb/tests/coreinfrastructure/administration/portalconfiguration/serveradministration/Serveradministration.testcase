@component-name = "portal-server-administration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Server Administration";

	setUp {
		User.firstLoginUI();
	}

	@description = "This is a use case for LPS-111757"
	@priority = "4"
	test ViewGlobalLibDirectory {
		property environment.acceptance = "quarantine";

		ServerAdministration.openServerAdmin();

		var script = '''
			import com.liferay.portal.kernel.util.PropsKeys;
			import com.liferay.portal.util.PropsUtil;

			out.println(PropsUtil.get(PropsKeys.${propsKey}));
		''';

		for (var propsKey : list "LIFERAY_SHIELDED_CONTAINER_LIB_PORTAL_DIR,LIFERAY_WEB_PORTAL_DIR") {
			ServerAdministration.executeScript(
				language = "Groovy",
				script = "${script}");

			if ("${propsKey}" == "LIFERAY_SHIELDED_CONTAINER_LIB_PORTAL_DIR") {
				var dirName = PropsUtil.get("liferay.app.server.shielded.container.lib.portal.dir");
			}
			else {
				var dirName = PropsUtil.get("liferay.app.server.portal.web.dir");
			}

			var dirEnding = StringUtil.endsWith("${dirName}", "/");

			if ("${dirEnding}" == "true") {
				AssertTextEquals(
					locator1 = "ServerAdministrationScript#OUTPUT_FIELD",
					value1 = "${dirName}");
			}
			else {
				AssertTextEquals(
					locator1 = "ServerAdministrationScript#OUTPUT_FIELD",
					value1 = "${dirName}/");
			}
		}
	}

	@description = "This test automates LPS-144283 and asserts that the Log Levels page displays search results even after navigating to a different page number"
	@priority = "4"
	test ViewLogLevelsDisplaySearchResultsAfterPageChange {
		task ("Given the admin user navigates to Log Levels in Server Administration") {
			ServerAdministration.openServerAdmin();

			Navigator.gotoNavItem(navItem = "Log Levels");
		}

		task ("When navigating to another page number and searching for a term") {
			Click(
				key_pageNumber = "2",
				locator1 = "Pagination#ANY_PAGE");

			Search.searchCP(searchTerm = "analytics");
		}

		task ("Then search results should still be present in the table") {
			AssertElementPresent(
				key_columnName = "lfr-level",
				key_tableEntry = "com.liferay.analytics",
				locator1 = "LexiconTable#TABLE_ENTRY_INFO");
		}
	}

	@description = "This is a use case for LPS-102790"
	@priority = "4"
	test ViewSystemAndPortalProperties {
		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Server Administration");

		ServerAdministration.viewProperties(
			propertyName = "catalina.home",
			propertyTitle = "System Properties");

		Pagination.changePagination(itemsPerPage = "20");

		ServerAdministration.viewProperties(
			propertyName = "admin.email.from.name",
			propertyTitle = "Portal Properties");

		Pagination.changePagination(itemsPerPage = "20");
	}

}