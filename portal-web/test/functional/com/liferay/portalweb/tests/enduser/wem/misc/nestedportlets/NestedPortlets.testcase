@component-name = "portal-util-misc"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Nested Portlets";
	property testray.main.component.name = "Nested Portlets";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "User can nest a widget inside Nested Applications widget."
	@priority = "5"
	test NestWidgetInsideNestedApplications {
		property portal.acceptance = "true";
		property testray.component.names = "Training";

		task ("Add a widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name");
		}

		task ("Change layout of a widget to 1 Column") {
			JSONLayout.updateLayoutTemplateOfPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				layoutTemplate = "1 Column");
		}

		task ("Add a Nested Applications and Web Content Display widget to page") {
			for (var widgetName : list "Nested Applications,Web Content Display") {
				JSONLayout.addWidgetToPublicLayout(
					groupName = "Test Site Name",
					layoutName = "Test Page Name",
					widgetName = "${widgetName}");
			}
		}

		task ("View default column on Nested Applications") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			NestedPortlets.viewConfigurationDefaultLayoutTemplate(
				defaultColumnName = "2 Columns (50/50)",
				portletName = "Nested Applications");
		}

		task ("Drag and drop Web Content Display to first column in Nested Applications") {
			NestedPortlets.dragAndDropPortletToPortletColumnPG(
				columnNumberFrom = "1",
				nestedColumnNumberTo = "1",
				portletName = "Web Content Display");
		}

		task ("View Web Content Display is nested to Nested Applications") {
			NestedPortlets.viewPG(
				nestedColumnNumber = "1",
				portletName = "Web Content Display");
		}
	}

}