@component-name = "portal-frontend-infrastructure"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "User Interface";
	property testray.main.component.name = "User Interface";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			PagesAdmin.tearDownCP();
		}
	}

	@description = "Verify the right to left localization css values are applied to a text field"
	@priority = "5"
	@refactordone
	test RTLDirectionStyleAppliedToTextField {
		property app.server.types = "tomcat";
		property database.types = "mysql";
		property environment.acceptance = "true";
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "RightToLeftInfrastructure#RTLDirectionStyleAppliedToTextField";
		property testray.component.names = "WYSIWYG";

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Web Content");

		WebContentNavigator.gotoAddCP();

		Navigator.ensureLocaleURL(locale = "ar");

		PortletEntry.changeLocale(locale = "ar-SA");

		WebContent.addCP(webContentTitle = "WC Title");

		WebContent.viewRTLLayout();

		Navigator.openURL();

		Navigator.ensureLocaleURL(locale = "en");
	}

	@description = "Verify the right to left localization css values are applied to page layout"
	@priority = "5"
	@refactordone
	test RTLStylesAppliedToPageLayout {
		property app.server.types = "tomcat";
		property database.types = "mysql";
		property environment.acceptance = "true";
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "RightToLeftInfrastructure#RTLStylesAppliedToPageLayout";
		property testray.component.names = "WYSIWYG";

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "RTL Test Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Guest",
			layoutName = "RTL Test Page",
			widgetName = "Language Selector");

		Navigator.gotoPage(pageName = "RTL Test Page");

		LanguagePortlet.selectLanguagePG(
			locale = "ar_SA",
			textIcon = "العربية-السعودية");

		ProductMenuHelper.openProductMenu();

		Page.viewRTLLayout();

		LanguagePortlet.selectLanguagePG(
			locale = "en_US",
			textIcon = "english-United States");
	}

}