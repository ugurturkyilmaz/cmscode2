@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Asset Publisher,Highest Rated Assets";
	property testray.main.component.name = "Highest Rated Assets";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "View rating score in Highest Rated Assets widget."
	@priority = "4"
	test ViewHighestRatedAsset {
		task ("Add two widget pages") {
			for (var i : list "1,2") {
				JSONLayout.addPublicLayout(
					groupName = "Test Site Name",
					layoutName = "Test Page Name ${i}");
			}
		}

		task ("Add a widget to each page") {
			var i = "1";

			for (var widgetName : list "Asset Publisher,Highest Rated Assets") {
				JSONLayout.addWidgetToPublicLayout(
					groupName = "Test Site Name",
					layoutName = "Test Page Name ${i}",
					widgetName = "${widgetName}");

				var i = ${i} + 1;
			}
		}

		task ("Enable ratings to each widget") {
			var i = "1";

			for (var portletName : list "Asset Publisher,Highest Rated Assets") {
				Navigator.gotoSitePage(
					pageName = "Test Page Name ${i}",
					siteName = "Test Site Name");

				AssetPublisherPortlet.configureDisplaySettings(
					displaySettings = "Ratings",
					portletName = "${portletName}");

				var i = ${i} + 1;
			}
		}

		task ("Add two web contents") {
			for (var i : list "1,2") {
				JSONWebcontent.addWebContent(
					content = "Web Content Content",
					groupName = "Test Site Name",
					title = "Web Content Title ${i}");
			}
		}

		task ("Add three stars to first web content on the first page") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name 1",
				siteName = "Test Site Name");

			AssetPublisherPortlet.viewAssetDetailsPG(
				assetContent = "Web Content Content",
				assetTitle = "Web Content Title 1");

			Ratings.rateStar(ratingStarCount = "3");
		}

		task ("Add five stars to second web content on the second page") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name 2",
				siteName = "Test Site Name");

			AssetPublisherPortlet.viewAssetDetailsPG(
				assetContent = "Web Content Content",
				assetTitle = "Web Content Title 2");

			Ratings.rateStar(ratingStarCount = "5");
		}

		task ("View rating stars are shown on Highest Rated Assets") {
			var i = "1";

			for (var starCount : list "3,5") {
				Navigator.gotoSitePage(
					pageName = "Test Page Name 2",
					siteName = "Test Site Name");

				AssetPublisherPortlet.viewAssetDetailsPG(
					assetContent = "Web Content Content",
					assetTitle = "Web Content Title ${i}");

				Ratings.viewRateStar(
					averageRatingStarCount = "${starCount}.0",
					ratingStarCount = "${starCount}");

				var i = ${i} + 1;
			}
		}
	}

}