@component-name = "portal-staging"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Export/Import Framework";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Publishing",
			portlet = "Export");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Site.tearDownCP();
		}
	}

	@priority = "4"
	test DeleteExportTemplate {
		LAR.addExportTemplateCP(
			exportTemplateDescription = "Export Template Description",
			exportTemplateName = "Export Template Name");

		LAR.saveExportTemplateCP(
			exportTemplateDescription = "Export Template Description",
			exportTemplateName = "Export Template Name");

		LAR.viewExportTemplateCP(
			exportTemplateDescription = "Export Template Description",
			exportTemplateName = "Export Template Name");

		LAR.moveToTheRecycleBinExportTemplate(exportTemplateName = "Export Template Name");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Recycle Bin",
			portlet = "Recycle Bin");

		RecycleBin.viewCP(
			assetName = "Export Template Name",
			assetType = "Export Import Template",
			deleteUser = "Test Test");

		RecycleBin.restoreCP(assetName = "Export Template Name");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Publishing",
			portlet = "Export");

		LexiconEntry.gotoHeaderMenuItem(menuItem = "Export Templates");

		LAR.viewExportTemplateCP(
			exportTemplateDescription = "Export Template Description",
			exportTemplateName = "Export Template Name");
	}

	@priority = "3"
	test ExportWithRelaunchedProcess {
		LAR.exportSiteCP(siteScopeName = "Guest");

		var key_larFileName = "${larFileName}";

		AssertTextEquals(
			locator1 = "ExportImport#CURRENT_AND_PREVIOUS_STATUS_1",
			value1 = "Successful");

		var valueName = selenium.getText("//dl/dd/div/h2");

		echo("value name ${valueName}");

		var larFileName = RegexUtil.replace("${valueName}", "([a-zA-z0-9_.-]*)[(0-9KB)]*", "1");

		echo("lar file name ${larFileName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Relaunch",
			rowEntry = "${larFileName}");

		AssertTextEquals(
			locator1 = "ExportImport#CURRENT_AND_PREVIOUS_STATUS_1",
			value1 = "Successful");
	}

	@priority = "4"
	test SearchExportTemplate {
		LAR.addExportTemplateCP(
			exportTemplateDescription = "Export Template Description",
			exportTemplateName = "Export Template Name");

		LAR.saveExportTemplateCP(
			exportTemplateDescription = "Export Template Description",
			exportTemplateName = "Export Template Name");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Publishing",
			portlet = "Export");

		LexiconEntry.gotoHeaderMenuItem(menuItem = "Export Templates");

		LAR.searchTemplate(
			exportTemplateDescription = "Export Template Description",
			exportTemplateName = "Export Template Name");
	}

}