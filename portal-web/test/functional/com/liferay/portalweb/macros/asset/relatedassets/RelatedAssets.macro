definition {

	macro _selectAssetType {
		var key_assetType = "${assetType}";

		AssertClick(
			locator1 = "AssetRelatedAssets#RELATED_ASSETS_SELECT",
			value1 = "Select");

		var assetTypeLocator = StringUtil.replace("${assetType}", " ", "_");
		var assetTypeLocator = StringUtil.upperCase("${assetTypeLocator}");

		MenuItem.click(menuItem = "${assetType}");
	}

	macro gotoBookmarksSelectionPG {
		Portlet.clickPortletPlusIconPG(portletName = "Bookmarks");

		MenuItem.click(menuItem = "Bookmark");

		Panel.expandPanel(panel = "Related Assets");

		AssertClick(
			locator1 = "AssetRelatedAssets#RELATED_ASSETS_SELECT",
			value1 = "Select");

		var key_assetType = "${assetType}";

		AssertClick(
			locator1 = "APConfiguration#ASSET_SELECTION_MANUAL_ASSET_ENTRIES_ASSET",
			value1 = "${assetType}");
	}

	macro removeAsset {
		Panel.expandPanel(panel = "Related Assets");

		for (var removeAssetTitle : list "${removeAssetTitle}") {
			var key_assetTitle = "${removeAssetTitle}";

			Click(locator1 = "AssetRelatedAssets#RELATED_ASSETS_TABLE_REMOVE_LINK");

			AssertElementNotPresent(locator1 = "AssetRelatedAssets#RELATED_ASSETS_TABLE_REMOVE_LINK");
		}
	}

	macro selectAsset {
		Panel.expandPanel(panel = "Related Assets");

		RelatedAssets._selectAssetType(assetType = "${assetType}");

		SelectFrame(value1 = "relative=top");

		SelectFrame(
			key_title = "Select ${assetType}",
			locator1 = "IFrame#MODAL_ANY");

		if (isSet(scopeSite)) {
			Click(locator1 = "Dropdown#FILTER_AND_ORDER");

			MenuItem.click(menuItem = "${scopeSite}");
		}

		if (isSet(assetAll)) {
			PortletEntry.selectAll();
		}
		else {
			var key_assetTitle = "${assetTitle}";

			if (isSet(depotName)) {
				AssertClick(
					key_breadcrumbName = "Sites and Libraries",
					locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
					value1 = "Sites and Libraries");

				AssertClick(
					key_groupTab = "Asset Library",
					locator1 = "ItemSelector#NAVIGATION_GROUP_TAB",
					value1 = "Asset Library");

				LexiconCard.clickCard(card = "${depotName}");
			}

			Click(
				key_assetTitle = "${assetTitle}",
				locator1 = "AssetRelatedAssets#ASSET_TABLE_CHECKBOX");
		}

		SelectFrame(value1 = "relative=top");

		Button.clickDone();
	}

	macro viewDefaultPG {
		var key_portletName = "Related Assets";

		AssertTextEquals(
			locator1 = "Portlet#TITLE",
			value1 = "Related Assets");

		AssertTextEquals(
			key_widgetName = "Related Assets",
			locator1 = "Message#INFO_SPECIFIC_WIDGET",
			value1 = "There are no related assets available. This application will show all assets related to the main asset of the current page.");
	}

	macro viewNoPG {
		for (var assetTitle : list "${assetTitleList}") {
			var key_assetTitle = "${assetTitle}";

			AssertElementNotPresent(locator1 = "Relatedassets#ASSET_TITLE");
		}
	}

	macro viewPG {
		if (!(isSet(rowNumber))) {
			var rowNumber = "1";
		}

		if (IsElementPresent(locator1 = "Relatedassets#ASSET_ENTRY_STICKER")) {
			AssertVisible(locator1 = "Relatedassets#ASSET_ENTRY_STICKER");
		}

		AssertTextEquals(
			key_assetTitle = "${relatedAssetTitle}",
			locator1 = "Relatedassets#ASSET_TITLE",
			value1 = "${relatedAssetTitle}");
	}

}