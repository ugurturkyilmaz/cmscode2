definition {

	var validationErrorMessage = "This field is required.";

	macro changeLocale {
		Click(locator1 = "FormPortlet#LOCALIZATION_BUTTON");

		Click(
			key_locale = "${locale}",
			locator1 = "FormPortlet#LOCALIZATION_OPTION");
	}

	macro clickNextPage {
		AssertClick(
			locator1 = "Button#NEXT",
			value1 = "Next");
	}

	macro clickSubmit {
		AssertClick(
			locator1 = "FormPortlet#SUBMIT_BUTTON",
			value1 = "Submit");
	}

	macro nextPage {
		AssertClick(
			locator1 = "Button#NEXT",
			value1 = "Next");

		var key_formPageName = "${formPageCurrent}";

		AssertTextEquals(
			locator1 = "FormPortlet#FORM_PAGE_INACTIVE",
			value1 = "${formPageCurrent}");

		var key_formPageName = "${formPageNext}";

		AssertTextEquals(
			locator1 = "FormPortlet#FORM_PAGE_ACTIVE",
			value1 = "${formPageNext}");
	}

	macro previousPage {
		Click(locator1 = "Button#PREVIOUS");
	}

	macro submit {
		if (IsElementPresent(locator1 = "FormPortlet#PORTLET_CONTENT")) {
			Click(locator1 = "FormPortlet#PORTLET_CONTENT");
		}

		WaitForElementPresent(locator1 = "FormPortlet#SUBMIT_BUTTON");

		FormPortlet.clickSubmit();
	}

	macro submitLocalizedSuccessfully {
		AssertClick(
			locator1 = "Button#MAIN_CONTENT_SUBMIT",
			value1 = "${buttonName}");

		WaitForElementPresent(locator1 = "Message#SUCCESS_DISMISSIBLE");

		Alert.viewSuccessMessage();
	}

	macro submitSuccessfully {
		FormPortlet.submit();

		WaitForElementPresent(locator1 = "Message#SUCCESS_DISMISSIBLE");

		Alert.viewSuccessMessage();
	}

	macro submitSuccessfullyAndViewSuccessPage {
		FormPortlet.submitSuccessfully();

		if (!(isSet(successPageDescription))) {
			var successPageDescription = "Your information was successfully received. Thank you for filling out the form.";
		}

		if (!(isSet(successPageTitle))) {
			var successPageTitle = "Thank you.";
		}

		FormPortlet.viewSuccessPage(
			successPageDescription = "${successPageDescription}",
			successPageTitle = "${successPageTitle}");
	}

	macro submitWithError {
		FormPortlet.submit();

		WaitForElementPresent(
			locator1 = "Message#ERROR",
			value1 = "${errorMessage}");

		Alert.viewErrorMessage(errorMessage = "${errorMessage}");
	}

	macro submitWithValidationError {
		FormPortlet.submit();

		FormPortlet.viewValidationErrorMessage(validationErrorMessage = "${validationErrorMessage}");
	}

	macro viewSuccessPage {
		var key_successPageTitle = "${successPageTitle}";

		AssertTextEquals(
			locator1 = "FormPortlet#SUCCESS_PAGE_TITLE",
			value1 = "${successPageTitle}");

		var key_successPageDescription = "${successPageDescription}";

		AssertTextEquals(
			locator1 = "FormPortlet#SUCCESS_PAGE_DESCRIPTION",
			value1 = "${successPageDescription}");
	}

	macro viewValidationErrorMessage {
		WaitForElementPresent(
			locator1 = "Message#ERROR_FORM_FIELD_REQUIRED",
			value1 = "${validationErrorMessage}");

		AssertTextEquals(
			locator1 = "Message#ERROR_FORM_FIELD_REQUIRED",
			value1 = "${validationErrorMessage}");
	}

	macro waitFormRenderer {
		var key_text = "${text}";

		WaitForElementPresent(locator1 = "Button#ANY_ENABLED");

		Pause(locator1 = "2500");
	}

	macro waitForValidationErrorNotPresent {
		WaitForElementNotPresent(
			locator1 = "Message#ERROR_FORM_FIELD_REQUIRED",
			value1 = "${validationErrorMessage}");
	}

}