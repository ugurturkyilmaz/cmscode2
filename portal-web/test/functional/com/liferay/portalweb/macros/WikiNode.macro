definition {

	var assetType = "Wiki Node";

	macro addCP {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${wikiNodeName}");

		Button.clickSave();

		var key_wikiNodeName = "${wikiNodeName}";

		Alert.viewSuccessMessage();
	}

	macro addMainCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Wiki");

		if (IsElementNotPresent(locator1 = "Wiki#NODE_TABLE")) {
			WikiNode.addCP(wikiNodeName = "Main");
		}
	}

	macro addWikiPage {
		var key_wikiPageTitle = "${wikiPageTitle}";
		var key_wikiPageContent = "${wikiPageContent}";

		Click(locator1 = "Button#PLUS");

		PortletEntry.inputTitle(title = "${wikiPageTitle}");

		SelectFrame.selectFrameNoLoading(
			key_title = "Rich Text Editor",
			locator1 = "IFrame#MODAL_ANY");

		Type(
			locator1 = "CKEditor#BODY",
			value1 = "${wikiPageContent}");

		SelectFrame.selectFrameNoLoading(locator1 = "TextArea#ANY_LABELED");
	}

	macro deleteCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		var key_wikiNodeName = "${wikiNodeName}";

		AssertTextEquals(
			locator1 = "Wiki#NODE_TABLE_WIKI_NAME",
			value1 = "${wikiNodeName}");

		Click(locator1 = "Wiki#NODE_TABLE_ACTIONS_DROPDOWN");

		PortletEntry.clickMoveToRecycleBin();

		VerifyElementPresent(locator1 = "Message#SUCCESS");
	}

	macro editCP {
		var key_wikiNodeName = "${wikiNodeName}";

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "Wiki#NODE_TABLE_WIKI_NAME",
			value1 = "${wikiNodeName}");

		Click(locator1 = "Wiki#NODE_TABLE_ACTIONS_DROPDOWN");

		MenuItem.click(menuItem = "Edit");

		PortletEntry.inputName(name = "${wikiNodeNameEdit}");

		PortletEntry.inputDescription(description = "${wikiNodeDescriptionEdit}");

		Button.clickSave();

		var key_wikiNodeName = "${wikiNodeNameEdit}";

		Alert.viewSuccessMessage();
	}

	macro importWikiPage {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Import Pages",
			rowEntry = "${wikiNodeName}");

		var dependencyFilePath = TestCase.getDependenciesDirPath(fileName = "${fileName}");

		UploadFile(
			key_tag = "Pages File",
			locator1 = "Wiki#IMPORT_PAGES_MEDIAWIKI",
			value1 = "${dependencyFilePath}");

		Click(
			key_text = "Import",
			locator1 = "Button#ANY");

		Alert.viewSuccessMessage();
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Wiki");

		if (IsElementPresent(locator1 = "Wiki#NODE_TABLE_ACTIONS_GENERIC_DROPDOWN_2")) {
			ManagementBar.clickSelectAllCheckbox();

			Click(locator1 = "Icon#DELETE");
		}

		JSONRecycleBin.emptyRecycleBin();
	}

	macro viewCP {
		var key_wikiNodeName = "${wikiNodeName}";

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "Wiki#NODE_TABLE_WIKI_NAME",
			value1 = "${wikiNodeName}");

		AssertTextEquals(
			locator1 = "Wiki#NODE_TABLE_NUMBER_OF_PAGES",
			value1 = "${numberOfWikiPages}");

		AssertClick(
			locator1 = "Wiki#NODE_TABLE_WIKI_NAME",
			value1 = "${wikiNodeName}");

		SiteAdmin.viewPortletTitle(portletTitle = "Wiki");
	}

	macro viewNoCP {
		AssertElementNotPresent(
			key_wikiNodeName = "${wikiNodeName}",
			locator1 = "Wiki#NODE_TABLE_WIKI_NAME");
	}

}