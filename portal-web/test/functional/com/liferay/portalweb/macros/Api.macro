definition {

	var password = "test";
	var userEmailAddress = "test@liferay.com";

	macro apiJsonwsUserView {
		Open(locator1 = "Home#API_JSON_URL");

		Click(locator1 = "JSONWS#USER_UPDATE_USER");

		AssertTextEquals(
			locator1 = "JSONWSUser#API_METHOD",
			value1 = "/user/update-user");

		AssertElementPresent(locator1 = "Button#INVOKE");
	}

	macro editJsonwsUserView {
		AssertTextEquals(
			locator1 = "JSONWSUser#API_METHOD",
			value1 = "/user/update-user");

		Type(
			locator1 = "JSONWSUser#API_EXECUTE_USER_ID_FIELD",
			value1 = "${userId}");

		Click(locator1 = "JSONWSUser#API_EXECUTE_PASSWORD_RESET_FALSE_RADIO");

		Type(
			locator1 = "JSONWSUser#API_EXECUTE_SCREEN_NAME_FIELD",
			value1 = "${userScreenName}");

		Type(
			locator1 = "JSONWSUser#API_EXECUTE_EMAIL_ADDRESS_FIELD",
			value1 = "${userEmailAddress}");

		if (IsElementPresent(locator1 = "JSONWSUser#API_EXECUTE_FACEBOOK_ID_FIELD")) {
			Type(
				locator1 = "JSONWSUser#API_EXECUTE_FACEBOOK_ID_FIELD",
				value1 = "${userFacebookId}");
		}

		Type(
			locator1 = "JSONWSUser#API_EXECUTE_FIRST_NAME_FIELD",
			value1 = "${userFirstName}");

		Type(
			locator1 = "JSONWSUser#API_EXECUTE_LAST_NAME_FIELD",
			value1 = "${userLastName}");

		Type(
			locator1 = "JSONWSUser#API_EXECUTE_PREFIX_ID_FIELD",
			value1 = "${userPrefixId}");

		Type(
			locator1 = "JSONWSUser#API_EXECUTE_SUFFIX_ID_FIELD",
			value1 = "${userSuffixId}");

		Click(locator1 = "JSONWSUser#API_EXECUTE_MALE_TRUE_RADIO");

		Type(
			locator1 = "JSONWSUser#API_EXECUTE_BIRTHDAY_MONTH_FIELD",
			value1 = "${userBirthdayMonth}");

		Type(
			locator1 = "JSONWSUser#API_EXECUTE_BIRTHDAY_DAY_FIELD",
			value1 = "${userBirthdayDay}");

		Type(
			locator1 = "JSONWSUser#API_EXECUTE_BIRTHDAY_YEAR_FIELD",
			value1 = "${userBirthdayYear}");

		for (var userRoleTitle : list "${userRoleTitles}") {
			var roleId = JSONRoleAPI._getRoleIdByName(name = "${userRoleTitle}");

			Type.sendKeys(
				locator1 = "JSONWSUser#API_EXECUTE_ROLE_IDS_FIELD",
				value1 = "${roleId},");
		}

		KeyPress(
			locator1 = "JSONWSUser#API_EXECUTE_ROLE_IDS_FIELD",
			value1 = "\BACK_SPACE");

		AssertClick(
			locator1 = "Button#INVOKE",
			value1 = "Invoke");
	}

	macro getPAuth {
		Open(locator1 = "Home#API_JSON_URL");

		Click(locator1 = "JSONWS#ADDRESS_ADD_ADDRESS");

		AssertTextEquals(
			locator1 = "JSONWSUser#API_METHOD",
			value1 = "/address/add-address");

		var pAuth = selenium.getElementValue("JSONWSAddress#P_AUTH_FIELD");

		return "${pAuth}";
	}

	macro gotoPAuthURL {
		var pAuth = Api.getPAuth();

		Navigator.openSpecificURL(url = "${url}${pAuth}");
	}

}