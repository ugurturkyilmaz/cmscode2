definition {

	macro configureCustomization {
		Click(locator1 = "Icon#STAGING_BAR_VERTICAL_ELLIPSIS");

		if ("${menuItem}" == "Reset My Customizations") {
			MenuItem.clickNoError(menuItem = "Reset My Customizations");

			AssertConfirm(value1 = "Are you sure you want to reset your customizations to default?");
		}
		else {
			MenuItem.click(menuItem = "${menuItem}");

			if ("${menuItem}" == "View Page without my customizations") {
				AssertTextEquals.assertPartialText(
					locator1 = "Home#PAGE_CUSTOMIZATION_MESSAGE_INFO",
					value1 = "This is the default page without your customizations.");
			}
			else {
				AssertTextEquals.assertPartialText(
					locator1 = "Home#PAGE_CUSTOMIZATION_MESSAGE_INFO",
					value1 = "You can customize this page.");
			}
		}
	}

	macro enableSectionCustomizableCP {
		PagesAdmin.gotoPageEllipsisMenuItem(
			menuItem = "Configure",
			pageName = "${pageName}");

		Navigator.gotoNavTab(navTab = "Advanced");

		Panel.expandPanel(panel = "Customization Settings");

		Check.checkToggleSwitch(
			key_fieldLabel = "customizable",
			key_fieldsetLabel = "Customization Settings",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH_IN_FIELDSET");

		if (isSet(columnIdList)) {
			for (var columnId : list "${columnIdList}") {
				Check.toggleSwitch(
					key_toggleSwitchLabel = "column-${columnId}-customizable",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
			}
		}
		else {
			Check.toggleSwitch(
				key_toggleSwitchLabel = "column-${columnId}-customizable",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
		}

		PortletEntry.save();
	}

	macro viewCustomizablePagePG {
		AssertTextEquals.assertPartialText(
			locator1 = "Home#PAGE_CUSTOMIZATION_MESSAGE_INFO",
			value1 = "You can customize this page.");

		Click(locator1 = "Icon#STAGING_BAR_VERTICAL_ELLIPSIS");

		for (var menuItem : list "View Page without my customizations,Reset My Customizations") {
			MenuItem.viewPresent(menuItem = "${menuItem}");
		}

		Click(locator1 = "Icon#STAGING_BAR_VERTICAL_ELLIPSIS");
	}

}