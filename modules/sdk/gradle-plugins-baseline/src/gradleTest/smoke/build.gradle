import aQute.bnd.gradle.BundleTaskConvention
import aQute.bnd.osgi.Constants

import org.gradle.util.VersionNumber

buildscript {
	dependencies {
		classpath group: "biz.aQute.bnd", name: "biz.aQute.bnd.gradle", version: "4.3.0"
	}

	repositories {
		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

apply plugin: "com.liferay.baseline"

Properties bndProperties = _getBndProperties(project)

archivesBaseName = bndProperties[Constants.BUNDLE_SYMBOLICNAME]
group = "com.example"
version = bndProperties[Constants.BUNDLE_VERSION]

task runGradleTest

baseline {
	ignoreFailures = true
}

baselineConfiguration {
	allowMavenLocal = true
}

jar {
	BundleTaskConvention bundleTaskConvention = new BundleTaskConvention(jar)

	bundleTaskConvention.bndfile = file("bnd.bnd")

	convention.plugins.bundle = bundleTaskConvention

	doLast {
		bundleTaskConvention.buildBundle()
	}
}

repositories {
	mavenLocal()
}

runGradleTest {
	dependsOn baseline

	doLast {
		Properties finalBndProperties = _getBndProperties(project)

		assert finalBndProperties[Constants.BUNDLE_VERSION] == "2.0.0"

		File packageInfoFile = file("src/main/resources/com/example/sample/packageinfo")

		assert packageInfoFile.exists()
		assert packageInfoFile.text == "version 2.0.0"

		File generatedLogFile = new File(reporting.baseDir, "baseline/baseline.log")

		assert generatedLogFile.exists()

		File expectedLogFile = file(generatedLogFile.name + ".expected")

		assert expectedLogFile.text.normalize() == generatedLogFile.text.normalize()
	}
}

private Properties _getBndProperties(Project project) {
	Properties bndProperties = new Properties()

	File bndFile = project.file("bnd.bnd")

	bndFile.withInputStream {
		bndProperties.load it
	}

	bndProperties
}