apply plugin: "com.liferay.javadoc.formatter"
apply plugin: "java"

task runGradleTest

formatJavadoc {
	generateXml = true
	initializeMissingJavadocs = true
	updateJavadocs = true
}

repositories {
	File dir = rootProject.projectDir

	while (dir != null) {
		File tmpMavenRepositoryDir = new File(dir, ".m2-tmp")

		if (tmpMavenRepositoryDir.exists()) {
			maven {
				url tmpMavenRepositoryDir
			}
		}

		dir = dir.getParentFile();
	}

	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
}

runGradleTest {
	dependsOn formatJavadoc

	doLast {
		String[] fileNames = [
			"src/main/java/com/liferay/sample/GreetingBuilder.java",
			"src/main/resources/META-INF/javadocs-all.xml",
			"src/main/resources/META-INF/javadocs-rt.xml"
		]

		for (String fileName in fileNames) {
			File generatedFile = file(fileName)

			assert generatedFile.exists()

			File expectedFile = file(generatedFile.name + ".expected")

			assert expectedFile.text.normalize() == generatedFile.text.normalize()
		}
	}
}