apply plugin: "com.liferay.js.transpiler"
apply plugin: "java"

node {
	useNpm = true
}

task runGradleTest

dependencies {
	jsCompile group: "com.liferay", name: "com.liferay.frontend.js.web", version: "1.0.20"
}

repositories {
	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}
}

runGradleTest {
	dependsOn classes

	doLast {
		File expectedFile = file("node_modules/com.liferay.frontend.js.web/package.json")

		assert expectedFile.exists()
	}

	doLast {
		File dir = new File(sourceSets.main.output.resourcesDir, "META-INF/resources/math")

		for (String fileName in ["math.es.js", "math.es.js.map"]) {
			File generatedFile = new File(dir, fileName)

			assert generatedFile.exists()

			File expectedFile = file(generatedFile.name + ".expected")

			assert _normalize(expectedFile.text) == _normalize(generatedFile.text)
		}
	}
}

private String _normalize(String text) {
	String s = text.normalize()

	s = s.replace("\\r\\n", "\\n")

	s = s.replace("\\r", "\\n")

	return s.trim()
}