import com.liferay.gradle.util.copy.StripPathSegmentsAction

task buildEsModuleShims(type: Copy)

File jsDestinationDir = file("tmp/META-INF/resources")
File esModuleShimsDestinationDir = new File(jsDestinationDir, "es-module-shims")

buildEsModuleShims {
	dependsOn npmInstall

	doFirst {
		delete esModuleShimsDestinationDir
	}

	eachFile new StripPathSegmentsAction(2)

	from npmInstall.nodeModulesDir

	include "es-module-shims/dist/es-module-shims.js"

	includeEmptyDirs = false
	into esModuleShimsDestinationDir
}

classes {
	dependsOn buildEsModuleShims
}

clean {
	delete "tmp"
}

dependencies {
	compileOnly group: "com.liferay", name: "biz.aQute.bnd.annotation", version: "4.2.0.LIFERAY-PATCHED-2"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "default"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "3.0.1"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
	compileOnly group: "org.osgi", name: "org.osgi.namespace.extender", version: "1.0.1"
	compileOnly group: "org.osgi", name: "org.osgi.service.cm", version: "1.6.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.4.0"
	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"
	compileOnly group: "org.slf4j", name: "slf4j-api", version: "1.7.26"
	compileOnly project(":apps:frontend-js:frontend-js-importmaps-extender-api")
	compileOnly project(":apps:portal-url-builder:portal-url-builder-api")
	compileOnly project(":apps:static:portal-configuration:portal-configuration-metatype-api")
	compileOnly project(":core:petra:petra-function")
	compileOnly project(":core:petra:petra-string")
}