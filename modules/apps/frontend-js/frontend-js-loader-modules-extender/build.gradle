import com.liferay.gradle.util.copy.StripPathSegmentsAction

task buildLiferayAMDLoader(type: Copy)

File jsDestinationDir = file("tmp/META-INF/resources")

buildLiferayAMDLoader {
	dependsOn npmInstall
	eachFile new StripPathSegmentsAction(4)
	from npmInstall.nodeModulesDir

	include "@liferay/amd-loader/build/loader/loader.js.map"
	include "@liferay/amd-loader/build/loader/loader.js"

	includeEmptyDirs = false
	into jsDestinationDir
}

classes {
	dependsOn buildLiferayAMDLoader
}

clean {
	delete "tmp"
}

dependencies {
	compileInclude group: "com.github.yuchi", name: "npm-semver", version: "1.0.0"
	compileInclude group: "org.json", name: "json", version: "20180813"

	compileOnly group: "com.liferay", name: "biz.aQute.bnd.annotation", version: "4.2.0.LIFERAY-PATCHED-2"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "3.0.1"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
	compileOnly group: "org.osgi", name: "org.osgi.service.component", version: "1.4.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.4.0"
	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"
	compileOnly project(":apps:frontend-js:frontend-js-loader-modules-extender-api")
	compileOnly project(":apps:portal-url-builder:portal-url-builder-api")
	compileOnly project(":apps:static:osgi:osgi-util")
	compileOnly project(":apps:static:portal-configuration:portal-configuration-metatype-api")
	compileOnly project(":core:osgi-service-tracker-collections")
	compileOnly project(":core:petra:petra-concurrent")
	compileOnly project(":core:petra:petra-executor")
	compileOnly project(":core:petra:petra-lang")
	compileOnly project(":core:petra:petra-string")
}