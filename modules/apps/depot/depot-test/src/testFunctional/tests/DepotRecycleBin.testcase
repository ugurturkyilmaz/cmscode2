@component-name = "portal-document-management"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Depot";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONDepot.deleteDepot(depotName = "Test Depot Name");

			Site.tearDownCP();

			User.logoutPG();
		}
	}

	@description = "This ensures that the document can be deleted after the recycle bin is disabled."
	@priority = "5"
	@refactordone
	test CanDeleteDocumentAfterDisabling {
		JSONDocument.addFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Test Depot Name");

		DepotNavigator.openDepotSettingsGeneral(depotName = "Test Depot Name");

		Depot.configureRecycleBin(recycleBin = "Disabled");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		Depot.deleteContentViaBulkEditing(
			dmDocumentTitle = "DM Document Title",
			recycleBinDisabled = "true");
	}

	@description = "This ensures that the web content can be deleted after the recycle bin is disabled."
	@priority = "5"
	@refactordone
	test CanDeleteWCAfterDisabling {
		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepot(depotName = "Test Depot Name");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC WebContent Title");

		DepotNavigator.openDepotSettingsGeneral(depotName = "Test Depot Name");

		Depot.configureRecycleBin(recycleBin = "Disabled");

		DepotNavigator.openDepotWebContentAdmin(depotName = "Test Depot Name");

		AssertElementNotPresent(
			key_category = "Recycle Bin",
			locator1 = "ProductMenu#CATEGORY_COLLAPSED");

		WebContent.deleteWithDisabledRB(entryTitle = "WC WebContent Title");
	}

	@description = "This ensures that the documents can be restored from recycle bin."
	@priority = "5"
	@refactordone
	test CanRestoreDocuments {
		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_1",
			groupName = "Test Depot Name",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_2",
			groupName = "Test Depot Name",
			mimeType = "image/jpeg",
			sourceFileName = "Document_2.jpg");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		Depot.deleteContentViaBulkEditing(dmDocumentTitle = "Document_1,Document_2");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			depotName = "Test Depot Name",
			portlet = "Recycle Bin");

		RecycleBin.restoreCP(
			assetName = "Document_1",
			assetRestoreLocation = "Home",
			assetType = "Document",
			multipleAssets = "true");

		DepotNavigator.openDepotRecycleBinAdmin(depotName = "Test Depot Name");

		RecycleBin.restoreCP(
			assetName = "Document_2",
			assetRestoreLocation = "Home",
			assetType = "Document");

		for (var dmDocumentTitle : list "Document_1,Document_2") {
			DepotNavigator.openToDMEntryInDepot(
				depotName = "Test Depot Name",
				dmDocumentTitle = "${dmDocumentTitle}");

			DMDocument.viewCP(dmDocumentTitle = "${dmDocumentTitle}");
		}
	}

	@description = "This ensures that the web content can be restored from recycle bin."
	@priority = "5"
	@refactordone
	test CanRestoreWebContent {
		property portal.acceptance = "true";

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC WebContent Title");

		DepotNavigator.openDepotWebContentAdmin(depotName = "Test Depot Name");

		WebContent.moveToRecycleBinCP(
			assetType = "Web Content Article",
			webContentTitle = "WC WebContent Title");

		DepotNavigator.openDepotRecycleBinAdmin(depotName = "Test Depot Name");

		RecycleBin.restoreCP(
			assetName = "WC WebContent Title",
			assetRestoreLocation = "Home",
			assetType = "Web Content Article");

		DepotNavigator.openToEditWCEntry(
			depotName = "Test Depot Name",
			webContentTitle = "WC WebContent Title");

		WebContent.viewCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title");
	}

	@description = "This ensures that the expired entries can be removed automatically in the recycle bin."
	@priority = "5"
	@refactordone
	test ExpiredEntriesCanBeRemoved {
		property custom.properties = "trash.entry.check.interval=1${line.separator}trash.entries.max.age=1";

		JSONDocument.addFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Test Depot Name");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		DMDocument.deleteCP(dmDocumentTitle = "DM Document Title");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC WebContent Title");

		DepotNavigator.openDepotWebContentAdmin(
			depotName = "Test Depot Name",
			webContent = "true",
			webContentTitle = "WC WebContent Title");

		Depot.deleteContentViaBulkEditing(
			webContent = "true",
			webContentTitle = "WC WebContent Title");

		DepotNavigator.openDepotRecycleBinAdmin(depotName = "Test Depot Name");

		RecycleBin.viewCP(
			assetName = "DM Document Title",
			assetType = "Document",
			customMaxAge = "1 minute",
			deleteUser = "Test Test");

		RecycleBin.viewCP(
			assetName = "WC WebContent Title",
			assetType = "Web Content Article",
			customMaxAge = "1 minute",
			deleteUser = "Test Test");

		Pause(locator1 = "120000");

		Refresh();

		RecycleBin.viewDefaultCP();
	}

}