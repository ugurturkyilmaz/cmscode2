import com.liferay.gradle.plugins.patcher.PatchTask

apply plugin: "com.liferay.patcher"

task jarPatched(type: Zip)
task patch(type: PatchTask)

dependencies {
	compileOnly group: "net.sf.ehcache", name: "ehcache", transitive: false, version: "2.10.6"
	compileOnly group: "org.slf4j", name: "slf4j-api", transitive: false, version: "1.7.25"
}

jar {
	setActions([])

	dependsOn jarPatched
}

jarPatched {
	archiveName = jar.archiveName
	destinationDir = jar.destinationDir
	duplicatesStrategy = "exclude"

	exclude "rest-management-private-classpath/**"

	from sourceSets.main.output

	from {
		zipTree(
			configurations.compileOnly.find {
				it.name.startsWith "ehcache-"
			})
	}
}

patch {
	ext {
		autoClean = false
	}

	fileNames "net/sf/ehcache/CacheManager.java"

	originalLibModuleName = "ehcache"
}